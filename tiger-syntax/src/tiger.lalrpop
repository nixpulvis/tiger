use std::str::FromStr;
use ast::*;
use tok::{self, Tok};

grammar<'input>(text: &'input str)["LALR(1)"];

// Macros
/////////

List<T, D>: Vec<T> = {
    <v:(<T> D)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Repeat<T>: Vec<T> = {
    <v:(<T>)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

// Symbol
/////////

pub Symbol: Symbol = {
    "SYMBOL" => <>.into()
};

// Expression
/////////////

pub Expression: Box<Expression> = {
    OpenExp   => <>,
    ClosedExp => <>,
};

OpenExp: Box<Expression> = {
    if_open_exp  => <>,
    assign_exp   => <>,
    while_exp    => <>,
    for_exp      => <>,
    array_exp    => <>,
    negation_exp => <>,
};

ClosedExp: Box<Expression> = {
    nil_exp       => <>,
    break_exp     => <>,
    int_exp       => <>,
    string_exp    => <>,
    variable_exp  => <>,
    if_closed_exp => <>,
    sequence_exp  => <>,
    call_exp      => <>,
    operation_exp => <>,
    record_exp    => <>,
    let_exp       => <>,
    and_exp       => <>,
    or_exp        => <>,
};

#[inline]
nil_exp: Box<Expression> = {
    "NIL" => Box::new(Expression::Nil)
};

#[inline]
break_exp: Box<Expression> = {
    "BREAK" => Box::new(Expression::Break)
};

#[inline]
int_exp: Box<Expression> = {
    "INT" => Box::new(Expression::Int(i32::from_str(<>).unwrap())),
};

#[inline]
string_exp: Box<Expression> = {
    "STRING" => Box::new(Expression::String(<>.into()))
};

#[inline]
variable_exp: Box<Expression> = {
    Variable => Box::new(Expression::Variable(<>))
};

#[inline]
if_open_exp: Box<Expression> = {
    "IF" <test:Expression> "THEN" <t:Expression> => {
        Box::new(Expression::If {
            test: test,
            t: t,
            f: None,
        })
    },
    "IF" <test:Expression> "THEN" <t:ClosedExp> "ELSE" <f:OpenExp> => {
        Box::new(Expression::If {
            test: test,
            t: t,
            f: Some(f),
        })
    },
};

#[inline]
if_closed_exp: Box<Expression> = {
    "IF" <test:Expression> "THEN" <t:ClosedExp> "ELSE" <f:ClosedExp> => {
        Box::new(Expression::If {
            test: test,
            t: t,
            f: Some(f),
        })
    },
};

#[inline]
sequence_exp: Box<Expression> = {
    "LPAREN" <l:List<Expression, "SEMI">> "RPAREN" => Box::new(Expression::Sequence(l))
};

#[inline]
call_exp: Box<Expression> = {
    <s:Symbol> "LPAREN" <l:List<Expression, "COMMA">> "RPAREN" => Box::new(Expression::Call {
        ident: s,
        arguments: l,
    })
};

#[inline]
operation_exp: Box<Expression> = {
    "LPAREN" <l:Expression> <o:Operation> <r:Expression> "RPAREN" => Box::new(Expression::Operation {
        op: o,
        left: l,
        right: r,
    })
};

#[inline]
record_exp: Box<Expression> = {
    <s:Symbol> "LBRACE" <l:List<field_create, "COMMA">> "RBRACE" => Box::new(Expression::Record {
        fields: l,
        tdent: s,
    })
};

#[inline]
field_create: (Symbol, Box<Expression>) = {
    <s:Symbol> "EQ" <e:Expression> => (s, e)
};

#[inline]
assign_exp: Box<Expression> = {
    <v:Variable> "ASSIGN" <e:Expression> => Box::new(Expression::Assign {
        variable: v,
        expression: e,
    })
};

#[inline]
while_exp: Box<Expression> = {
    "WHILE" <t:Expression> "DO" <b:Expression> => Box::new(Expression::While {
        test: t,
        body: b,
    })
};

#[inline]
for_exp: Box<Expression> = {
    "FOR" <s:Symbol> "ASSIGN" <l:Expression> "TO" <h:Expression> "DO" <b:Expression> => {
        Box::new(Expression::For {
            ident: s,
            low: l,
            high: h,
            body: b,
        })
    }
};

#[inline]
let_exp: Box<Expression> = {
    "LET" <d:Repeat<Declaration>> "IN" <es:List<Expression, "SEMI">> "END" => {
        Box::new(Expression::Let {
            declarations: d,
            body: Box::new(Expression::Sequence(es)),
        })
    }
};

// FIXME: Don't use $ symbols.
#[inline]
array_exp: Box<Expression> = {
    "LPAREN" <s:Symbol> "HASH" <l:Expression> "HASH" "OF" <i:Expression> "RPAREN" => Box::new(Expression::Array {
        tdent: s,
        size: l,
        init: i,
    })
};

#[inline]
negation_exp: Box<Expression> = {
    "MINUS" <e:Expression> => Box::new(Expression::Operation {
        op: Operation::Minus,
        left: Box::new(Expression::Int(0)),
        right: e,
    })
};

#[inline]
and_exp: Box<Expression> = {
    "LPAREN" <e1:Expression> "AND" <e2:Expression> "RPAREN" => Box::new(Expression::If {
        test: e1,
        t: e2,
        f: Some(Box::new(Expression::Int(0))),
    })
};

#[inline]
or_exp: Box<Expression> = {
    "LPAREN" <e1:Expression> "OR" <e2:Expression> "RPAREN" => Box::new(Expression::If {
        test: e1,
        t: Box::new(Expression::Int(1)),
        f: Some(e2),
    })
};

// Operation
////////////

pub Operation: Operation = {
    "PLUS"   => Operation::Plus,
    "MINUS"  => Operation::Minus,
    "TIMES"  => Operation::Times,
    "DIVIDE" => Operation::Divide,
    "EQ"     => Operation::Eq,
    "NEQ"    => Operation::Neq,
    "LT"     => Operation::Lt,
    "LE"     => Operation::Le,
    "GT"     => Operation::Gt,
    "GE"     => Operation::Ge,
};

// Variable
///////////

pub Variable: Box<Variable> = {
    Symbol => Box::new(Variable::Simple(<>)),
    <v:Variable> "DOT" <s:Symbol> => Box::new(Variable::Field(v, s)),
    <v:Variable> "LBRACK" <e:Expression> "RBRACK" => Box::new(Variable::Subscript(v, e))
};

// Type
///////

pub Type: Box<Type> = {
    <s:Symbol> => Box::new(Type::Name(s)),
    "LBRACE" <l:List<field_dec, "COMMA">> "RBRACE" => Box::new(Type::Record(l)),
    "ARRAY" "OF" <s:Symbol> => Box::new(Type::Array(s)),
};

#[inline]
field_dec: (Symbol, Symbol) = {
    <i:Symbol> "COLON" <t:Symbol> => (i, t)
};

// Declaration
//////////////

pub Declaration: Box<Declaration> = {
	function_dec => <>,
	variable_dec => <>,
	type_dec => <>,
};

#[inline]
function_dec: Box<Declaration> = {
    "FUNCTION" <s:Symbol> "LPAREN" <l:List<field_dec, "COMMA">> "RPAREN" "EQ" <e:Expression> => {
        Box::new(Declaration::Function {
            ident: s,
            parameters: l,
            result: None,
            body: e,
        })
    },
    "FUNCTION" <s:Symbol> "LPAREN" <l:List<field_dec, "COMMA">> "RPAREN" "COLON" <t:Symbol> "EQ" <e:Expression> => {
        Box::new(Declaration::Function {
            ident: s,
            parameters: l,
            result: Some(t),
            body: e,
        })
    },
};

#[inline]
variable_dec: Box<Declaration> = {
    "VAR" <s:Symbol> "ASSIGN" <e:Expression> => Box::new(Declaration::Variable {
        ident: s,
        tdent: None,
        init: e,
    }),
    "VAR" <s:Symbol> "COLON" <t:Symbol> "ASSIGN" <e:Expression> => Box::new(Declaration::Variable {
        ident: s,
        tdent: Some(t),
        init: e,
    }),
};

#[inline]
type_dec: Box<Declaration> = {
    "TYPE" <s:Symbol> "EQ" <t:Type> => Box::new(Declaration::Type {
        tdent: s,
        ty: t,
    })
};

// Tokens
/////////

extern {
    type Location = usize;
    type Error = tok::Error;

    enum Tok<'input> {
        "SYMBOL" => Tok::Symbol(<&'input str>),
        "INT" => Tok::Int(<&'input str>),
        "STRING" => Tok::String(<&'input str>),

        "NIL" => Tok::Nil,
        "BREAK" => Tok::Break,
        "IF" => Tok::If,
        "THEN" => Tok::Then,
        "ELSE" => Tok::Else,
        "TYPE" => Tok::Type,
        "FUNCTION" => Tok::Function,
        "VAR" => Tok::Var,
        "ASSIGN" => Tok::Assign,
        "WHILE" => Tok::While,
        "FOR" => Tok::For,
        "TO" => Tok::To,
        "DO" => Tok::Do,
        "LET" => Tok::Let,
        "IN" => Tok::In,
        "END" => Tok::End,
        "HASH" => Tok::Hash,  // TODO: This is a hack for now.
        "ARRAY" => Tok::Array,
        "OF" => Tok::Of,
        "COLON" => Tok::Colon,
        "SEMI" => Tok::Semi,
        "COMMA" => Tok::Comma,
        "LPAREN" => Tok::LParen,
        "RPAREN" => Tok::RParen,
        "LBRACK" => Tok::LBrack,
        "RBRACK" => Tok::RBrack,
        "LBRACE" => Tok::LBrace,
        "RBRACE" => Tok::RBrace,
        "DOT" => Tok::Dot,
        "EQ" => Tok::Eq,
        "NEQ" => Tok::Neq,
        "AND" => Tok::And,
        "OR" => Tok::Or,
        "PLUS" => Tok::Plus,
        "MINUS" => Tok::Minus,
        "TIMES" => Tok::Times,
        "DIVIDE" => Tok::Divide,
        "LT" => Tok::Lt,
        "LE" => Tok::Le,
        "GT" => Tok::Gt,
        "GE" => Tok::Ge,
    }
}
